{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Adding user-defined influence, reaction model and GUI: The segregation model\n",
    "\n",
    "The segregation model has been proposed by [Thomas Schelling](https://en.wikipedia.org/wiki/Thomas_Schelling) in 1971 in his famous paper [Dynamic Models of Segregation](https://www.stat.berkeley.edu/~aldous/157/Papers/Schelling_Seg_Models.pdf). The goal of this model is to show that segregation can occur even if it is not wanted by the agents.\n",
    "\n",
    "In our implementation of this model, turtles are located in the grid and at each step, compute an happiness index based on the similarity of other agents in their neighborhood. If this index is below a value, called here similarity rate, the turtle wants to move to an other location.\n",
    "\n",
    "## Model parameters\n",
    "\n",
    "We define the following parameters and their default values.\n",
    "\n",
    "## Model-specific influence\n",
    "\n",
    "We define an influence called `Move` that is emitted by an agent who wants to move to another location. It is defined by a  unique identifier, here \"move\", and the state of the turtle that wants to move.\n",
    "\n",
    "## Decision model\n",
    "\n",
    "The decision model computes a happiness index based on the rate of turtles of different categories in its neighborhood. If the index is below the parameter `similarityRate`, the turtle emits a `Move` influence.\n",
    "\n",
    "## Reaction model\n",
    "\n",
    "The reaction model handles the `Move` influences emitted by unhappy turtles. First, it identifies vacant places and moves the turtles that have emitted a `Move` influence. Note that if there is not enough vacant places, not all turtle wishes can be fulfilled.\n",
    "\n",
    "## Simulation model\n",
    "\n",
    "The simulation model generates the Logo level using the user-defined reaction model and a simple periodic time model. It also generates turtles of 2 different types (a and b) randomly in the grid with respect to the vacancy rate parameter.\n",
    "\n",
    "## Launch the HTML runner\n",
    "\n",
    "The GUI is defined in a variable called `segregationgui`. Finally, we launch the web server with the above described GUI."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "metadata": {},
   "outputs": [
    {
     "name": "stderr",
     "output_type": "stream",
     "text": [
      "Downloaded 5481 Kbytes in 191ms:\n",
      "  [SUCCESSFUL ] fr.univ-artois.lgi2a#similar2logo-lib;1.0-SNAPSHOT!similar2logo-lib.jar (5ms)\n",
      "  [SUCCESSFUL ] fr.univ-artois.lgi2a#similar2logo-kernel;1.0-SNAPSHOT!similar2logo-kernel.jar (3ms)\n",
      "  [SUCCESSFUL ] net.jafama#jafama;2.3.1!jafama.jar (3ms)\n",
      "  [SUCCESSFUL ] fr.univ-artois.lgi2a#similar-microKernel;1.0-SNAPSHOT!similar-microKernel.jar (2ms)\n",
      "  [SUCCESSFUL ] fr.univ-artois.lgi2a#similar-microKernel-commonLibs;1.0-SNAPSHOT!similar-microKernel-commonLibs.jar (3ms)\n",
      "  [SUCCESSFUL ] fr.univ-artois.lgi2a#similar-extendedKernel;1.0-SNAPSHOT!similar-extendedKernel.jar (2ms)\n",
      "  [SUCCESSFUL ] fr.univ-artois.lgi2a#similar-extendedKernel-extendedLibs;1.0-SNAPSHOT!similar-extendedKernel-extendedLibs.jar (2ms)\n",
      "  [SUCCESSFUL ] org.apache.commons#commons-math3;3.6.1!commons-math3.jar (23ms)\n",
      "  [SUCCESSFUL ] org.json#json;20170516!json.jar(bundle) (4ms)\n",
      "  [SUCCESSFUL ] com.sparkjava#spark-core;2.8.0!spark-core.jar(bundle) (4ms)\n",
      "  [SUCCESSFUL ] org.slf4j#slf4j-api;1.7.25!slf4j-api.jar (3ms)\n",
      "  [SUCCESSFUL ] org.eclipse.jetty#jetty-server;9.4.12.v20180830!jetty-server.jar (10ms)\n",
      "  [SUCCESSFUL ] javax.servlet#javax.servlet-api;3.1.0!javax.servlet-api.jar (7ms)\n",
      "  [SUCCESSFUL ] org.eclipse.jetty#jetty-http;9.4.12.v20180830!jetty-http.jar (8ms)\n",
      "  [SUCCESSFUL ] org.eclipse.jetty#jetty-util;9.4.12.v20180830!jetty-util.jar (7ms)\n",
      "  [SUCCESSFUL ] org.eclipse.jetty#jetty-io;9.4.12.v20180830!jetty-io.jar (10ms)\n",
      "  [SUCCESSFUL ] org.eclipse.jetty#jetty-webapp;9.4.12.v20180830!jetty-webapp.jar (8ms)\n",
      "  [SUCCESSFUL ] org.eclipse.jetty#jetty-xml;9.4.12.v20180830!jetty-xml.jar (6ms)\n",
      "  [SUCCESSFUL ] org.eclipse.jetty#jetty-servlet;9.4.12.v20180830!jetty-servlet.jar (5ms)\n",
      "  [SUCCESSFUL ] org.eclipse.jetty#jetty-security;9.4.12.v20180830!jetty-security.jar (4ms)\n",
      "  [SUCCESSFUL ] org.eclipse.jetty.websocket#websocket-server;9.4.12.v20180830!websocket-server.jar (6ms)\n",
      "  [SUCCESSFUL ] org.eclipse.jetty.websocket#websocket-common;9.4.12.v20180830!websocket-common.jar (6ms)\n",
      "  [SUCCESSFUL ] org.eclipse.jetty.websocket#websocket-api;9.4.12.v20180830!websocket-api.jar (4ms)\n",
      "  [SUCCESSFUL ] org.eclipse.jetty.websocket#websocket-client;9.4.12.v20180830!websocket-client.jar (5ms)\n",
      "  [SUCCESSFUL ] org.eclipse.jetty#jetty-client;9.4.12.v20180830!jetty-client.jar (8ms)\n",
      "  [SUCCESSFUL ] org.eclipse.jetty.websocket#websocket-servlet;9.4.12.v20180830!websocket-servlet.jar (5ms)\n",
      "  [SUCCESSFUL ] org.slf4j#jul-to-slf4j;1.7.25!jul-to-slf4j.jar (3ms)\n",
      "  [SUCCESSFUL ] org.slf4j#jcl-over-slf4j;1.7.25!jcl-over-slf4j.jar (4ms)\n",
      "  [SUCCESSFUL ] org.slf4j#log4j-over-slf4j;1.7.25!log4j-over-slf4j.jar (4ms)\n",
      "  [SUCCESSFUL ] org.slf4j#slf4j-jdk14;1.7.25!slf4j-jdk14.jar (4ms)\n",
      "org.codehaus.groovy.control.MultipleCompilationErrorsException: startup failed:\n",
      "Script2.groovy: 35: unable to resolve class fr.univ_artois.lgi2a.similar.extendedkernel.libs.random.PRNG\n",
      " @ line 35, column 1.\n",
      "   import fr.univ_artois.lgi2a.similar.extendedkernel.libs.random.PRNG\n",
      "   ^\n",
      "\n",
      "1 error\n",
      "\n",
      "\tat org.codehaus.groovy.control.ErrorCollector.failIfErrors(ErrorCollector.java:310)\n",
      "\tat org.codehaus.groovy.control.CompilationUnit.applyToSourceUnits(CompilationUnit.java:958)\n",
      "\tat org.codehaus.groovy.control.CompilationUnit.doPhaseOperation(CompilationUnit.java:605)\n",
      "\tat org.codehaus.groovy.control.CompilationUnit.compile(CompilationUnit.java:554)\n",
      "\tat groovy.lang.GroovyClassLoader.doParseClass(GroovyClassLoader.java:298)\n",
      "\tat groovy.lang.GroovyClassLoader.parseClass(GroovyClassLoader.java:268)\n",
      "\tat groovy.lang.GroovyClassLoader.parseClass(GroovyClassLoader.java:254)\n",
      "\tat groovy.lang.GroovyClassLoader.parseClass(GroovyClassLoader.java:250)\n",
      "\tat org.scijava.plugins.scripting.groovy.GroovyScriptEngine.getScriptClass(GroovyScriptEngine.java:319)\n",
      "\tat org.scijava.plugins.scripting.groovy.GroovyScriptEngine.eval(GroovyScriptEngine.java:122)\n",
      "\tat javax.script.AbstractScriptEngine.eval(AbstractScriptEngine.java:264)\n",
      "\tat org.scijava.script.ScriptModule.run(ScriptModule.java:159)\n",
      "\tat org.scijava.module.ModuleRunner.run(ModuleRunner.java:167)\n",
      "\tat org.scijava.jupyter.kernel.evaluator.Worker.run(Worker.java:109)\n",
      "\tat org.scijava.thread.DefaultThreadService$2.run(DefaultThreadService.java:220)\n",
      "\tat java.util.concurrent.Executors$RunnableAdapter.call(Executors.java:511)\n",
      "\tat java.util.concurrent.FutureTask.run(FutureTask.java:266)\n",
      "\tat java.util.concurrent.ThreadPoolExecutor.runWorker(ThreadPoolExecutor.java:1142)\n",
      "\tat java.util.concurrent.ThreadPoolExecutor$Worker.run(ThreadPoolExecutor.java:617)\n",
      "\tat java.lang.Thread.run(Thread.java:745)\n"
     ]
    },
    {
     "data": {
      "text/plain": [
       "No Outputs"
      ]
     },
     "execution_count": 2,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "#@repository(\"~/.m2/repository/\")\n",
    "#@dependency(group=\"fr.univ-artois.lgi2a\", module=\"similar2logo-lib\", version=\"1.0-SNAPSHOT\")\n",
    "\n",
    "import static java.lang.Math.*\n",
    "import static fr.univ_artois.lgi2a.similar2logo.kernel.model.levels.LogoSimulationLevelList.LOGO\n",
    "\n",
    "import java.awt.geom.Point2D\n",
    "\n",
    "import fr.univ_artois.lgi2a.similar.extendedkernel.levels.ExtendedLevel\n",
    "import fr.univ_artois.lgi2a.similar.extendedkernel.libs.abstractimpl.AbstractAgtDecisionModel\n",
    "import fr.univ_artois.lgi2a.similar.extendedkernel.libs.timemodel.PeriodicTimeModel\n",
    "import fr.univ_artois.lgi2a.similar.extendedkernel.simulationmodel.ISimulationParameters\n",
    "import fr.univ_artois.lgi2a.similar.microkernel.AgentCategory\n",
    "import fr.univ_artois.lgi2a.similar.microkernel.LevelIdentifier\n",
    "import fr.univ_artois.lgi2a.similar.microkernel.SimulationTimeStamp\n",
    "import fr.univ_artois.lgi2a.similar.microkernel.ISimulationModel.AgentInitializationData\n",
    "import fr.univ_artois.lgi2a.similar.microkernel.agents.IGlobalState\n",
    "import fr.univ_artois.lgi2a.similar.microkernel.agents.ILocalStateOfAgent\n",
    "import fr.univ_artois.lgi2a.similar.microkernel.agents.IPerceivedData\n",
    "import fr.univ_artois.lgi2a.similar.microkernel.dynamicstate.ConsistentPublicLocalDynamicState\n",
    "import fr.univ_artois.lgi2a.similar.microkernel.influences.IInfluence\n",
    "import fr.univ_artois.lgi2a.similar.microkernel.influences.InfluencesMap\n",
    "import fr.univ_artois.lgi2a.similar.microkernel.influences.RegularInfluence\n",
    "import fr.univ_artois.lgi2a.similar.microkernel.levels.ILevel\n",
    "import fr.univ_artois.lgi2a.similar2logo.kernel.initializations.AbstractLogoSimulationModel\n",
    "import fr.univ_artois.lgi2a.similar2logo.kernel.model.LogoSimulationParameters\n",
    "import fr.univ_artois.lgi2a.similar2logo.kernel.model.Parameter\n",
    "import fr.univ_artois.lgi2a.similar2logo.kernel.model.agents.turtle.TurtleAgentCategory\n",
    "import fr.univ_artois.lgi2a.similar2logo.kernel.model.agents.turtle.TurtleFactory\n",
    "import fr.univ_artois.lgi2a.similar2logo.kernel.model.agents.turtle.TurtlePLSInLogo\n",
    "import fr.univ_artois.lgi2a.similar2logo.kernel.model.environment.LogoEnvPLS\n",
    "import fr.univ_artois.lgi2a.similar2logo.kernel.model.levels.LogoDefaultReactionModel\n",
    "import fr.univ_artois.lgi2a.similar2logo.lib.model.ConeBasedPerceptionModel\n",
    "import fr.univ_artois.lgi2a.similar2logo.lib.tools.html.Similar2LogoHtmlRunner\n",
    "import fr.univ_artois.lgi2a.similar.extendedkernel.libs.random.PRNG\n",
    "\n",
    "//Define the parameters of the simulation\n",
    "def parameters = new LogoSimulationParameters() {\n",
    "    @Parameter(\n",
    "        name = \"similarity rate\",\n",
    "        description = \"the rate of same-color turtles that each turtle wants among its neighbors\"\n",
    "    )\n",
    "    public double similarityRate = 3.0/8\n",
    "\n",
    "    @Parameter(name = \"vacancy rate\", description = \"the rate of vacant settling places\")\n",
    "    public double vacancyRate = 0.05\n",
    "\n",
    "    @Parameter(name = \"perception distance\", description = \"the perception distance of agents\")\n",
    "    public double perceptionDistance = sqrt(2)\n",
    "}\n",
    "\n",
    "//Define the specific influence of this model\n",
    "class Move extends RegularInfluence {\n",
    "    \n",
    "    //The turtle's public local state that is going to change\n",
    "    def target \n",
    "    \n",
    "    //the category of the influence, used as a unique identifier in the reaction to determine the nature of the influence\n",
    "    static final def CATEGORY = \"move\"\n",
    "        \n",
    "    Move(SimulationTimeStamp s, SimulationTimeStamp ns, TurtlePLSInLogo target) {\n",
    "        super(CATEGORY, LOGO, s, ns)\n",
    "        this.target = target\n",
    "    }\n",
    "}\n",
    "\n",
    "//Define the decision model of an agent\n",
    "def decisionModel = new AbstractAgtDecisionModel(LOGO) {\n",
    "    void decide(\n",
    "        SimulationTimeStamp s, //the current simulation step\n",
    "        SimulationTimeStamp ns, //the next simulation step\n",
    "        IGlobalState gs, //the global state of the agent\n",
    "        ILocalStateOfAgent pls, //the public local state of the agent\n",
    "        ILocalStateOfAgent prls, //the private local state of the agent\n",
    "        IPerceivedData pd, //the data perceived by the agent\n",
    "        InfluencesMap i //the influences produced by the agent\n",
    "    ) {\n",
    "        //compute the similarity rate\n",
    "        def sr = 0\n",
    "        pd.turtles.each{ agent -> if(agent.content.categoryOfAgent.isA(pls.categoryOfAgent)) sr++ } \n",
    "        if(!pd.turtles.empty) sr/= pd.turtles.size()\n",
    "        //if the similarity rate is too low, the agent wants to move\n",
    "        if(sr < parameters.similarityRate) i.add new Move(s, ns, pls)\n",
    "    }\n",
    "}\n",
    "\n",
    "//Define the reaction model\n",
    "def reactionModel = new LogoDefaultReactionModel() {\n",
    "    //redefine the reaction function for regular influences\n",
    "    public void makeRegularReaction(\n",
    "        SimulationTimeStamp s, //the current simulation step\n",
    "        SimulationTimeStamp ns, //the next simulation step\n",
    "        ConsistentPublicLocalDynamicState cs, //the dynamic state of the simulation\n",
    "        Set<IInfluence> influences, //the influences to process \n",
    "        InfluencesMap remainingInfluences //the influences that will remain i the dynamic state\n",
    "    ) {\n",
    "        def e = cs.publicLocalStateOfEnvironment, //the environment\n",
    "            li = [], //the list of influences\n",
    "            vacant = [] //the list of vacant housings\t\n",
    "        li.addAll influences //create the list of influences\n",
    "        PRNG.shuffle li //shuffle the list of influences\n",
    "        for(x in 0..<e.width) for(y in 0..<e.height)\n",
    "            if(e.getTurtlesAt(x, y).empty) vacant.add new Point2D.Double(x,y) //identify vacant housings\n",
    "        PRNG.shuffle vacant //shuffle the list of vacant housings\n",
    "        def n = 0 \n",
    "        li.any{ i -> //move lucky unhappy agents to vacant housings\n",
    "            if(i.category == Move.CATEGORY) {\n",
    "                e.turtlesInPatches[(int) i.target.location.x][(int) i.target.location.y].remove i.target\n",
    "                e.turtlesInPatches[(int) vacant[n].x][(int) vacant[n].y].add i.target\n",
    "                i.target.setLocation(vacant[n])\n",
    "                if(++n >= vacant.size()) return true //stop when no more housing is available\n",
    "            }\n",
    "        }\n",
    "    }\n",
    "}\n",
    "\n",
    "//Define the simulation model\n",
    "def simulationModel = new AbstractLogoSimulationModel(parameters) {\n",
    "    \n",
    "    List<ILevel> generateLevels(ISimulationParameters p) {\n",
    "        def logo = new ExtendedLevel(\n",
    "            p.initialTime,\n",
    "            LOGO,\n",
    "            new PeriodicTimeModel(1,0, p.initialTime),\n",
    "            reactionModel\n",
    "        )\n",
    "        def levelList = []\n",
    "        levelList.add logo\n",
    "        return levelList\n",
    "    }\n",
    "\n",
    "    AgentInitializationData generateAgents(ISimulationParameters p, Map<LevelIdentifier, ILevel> l) {\n",
    "        def result = new AgentInitializationData()\n",
    "        for(x in 0..<p.gridWidth) for(y in 0..<p.gridHeight)\n",
    "            if(PRNG.randomDouble() >= p.vacancyRate) result.agents.add TurtleFactory.generate(\n",
    "                new ConeBasedPerceptionModel(p.perceptionDistance, 2*PI, true, false, false),\n",
    "                decisionModel,\n",
    "                new AgentCategory(PRNG.randomBoolean() ? \"a\" :\"b\", TurtleAgentCategory.CATEGORY),\n",
    "                0, 0, 0, x,y\n",
    "            )\n",
    "        return result\n",
    "    }\n",
    "}\n",
    "\n",
    "//Define the custom GUI\n",
    "def segregationgui = '''\n",
    "    <canvas id='grid_canvas' class='center-block' width='400' height='400'></canvas>\n",
    "    <script type='text/javascript'>\n",
    "        drawCanvas = function (data) {\n",
    "            var json = JSON.parse(data),\n",
    "                canvas = document.getElementById('grid_canvas'),\n",
    "                context = canvas.getContext('2d');\n",
    "            context.clearRect(0, 0, canvas.width, canvas.height);\n",
    "            for (var i = 0; i < json.agents.length; i++) {\n",
    "                var centerX = json.agents[i].x * canvas.width;\n",
    "                var centerY = json.agents[i].y * canvas.height;\n",
    "                var radius = 2;\n",
    "                if (json.agents[i].t == 'a') {\n",
    "                    context.fillStyle = 'red';\n",
    "                } else {\n",
    "                    context.fillStyle = 'blue';\n",
    "                }\n",
    "                context.beginPath();\n",
    "                context.arc(centerX, centerY, radius, 0, 2 * Math.PI, false);\n",
    "                context.fill();\n",
    "            }\n",
    "        }\n",
    "    </script>'''\n",
    "\n",
    "//Creation of the runner\n",
    "def runner = new Similar2LogoHtmlRunner( )\n",
    "\n",
    "//Configuration of the runner\n",
    "runner.config.exportAgents = true\n",
    "\n",
    "// Initialize the GUI\n",
    "runner.config.setCustomHtmlBodyFromString segregationgui\n",
    "\n",
    "// Initialize the runner\n",
    "runner.initializeRunner simulationModel\n",
    "\n",
    "// Open the GUI\n",
    "runner.showView( )"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "SciJava",
   "language": "groovy",
   "name": "scijava"
  },
  "language_info": {
   "codemirror_mode": "groovy",
   "file_extension": "",
   "mimetype": "",
   "name": "scijava",
   "nbconverter_exporter": "",
   "pygments_lexer": "groovy",
   "version": "1.0"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
