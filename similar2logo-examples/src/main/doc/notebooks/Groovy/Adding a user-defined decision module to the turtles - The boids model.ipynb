{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Adding a user-defined decision module to the turtles: The boids model\n",
    "\n",
    "The [boids](https://en.wikipedia.org/wiki/Boids) (bird-oid) model has been invented by [Craig Reynolds](https://en.wikipedia.org/wiki/Craig_Reynolds_(computer_graphics)) in 1986 to simulate the flocking behavior of birds. It is based on 3 principles:\n",
    "    \n",
    "* separation: boids tend to avoid other boids that are too close,\n",
    "\n",
    "* alignment: boids tend to align their velocity to boids that are not too close and not too far away,\n",
    "\n",
    "* cohesion: bois tend to move towards boids that are too far away.\n",
    "\n",
    "While these rules are essentially heuristic, they can be implemented defining three areas (repulsion, orientation, attraction) for each principle. \n",
    "\n",
    "* Boids change their orientation to get away from other boids in the repulsion area,\n",
    "\n",
    "* Boids change their orientation and speed to match those of other boids in the orientation area,\n",
    "\n",
    "* Boids change their orientation to get to other boids in the attraction area.\n",
    "\n",
    "## Model parameters\n",
    "\n",
    "The model parameters and their default values are defined\n",
    "\n",
    "## Decision model\n",
    "\n",
    "The decision model consists in changing the direction and speed of the boids according to the previously described rules.\n",
    "To define a decision model, the modeler must define an object that extends `AbstractAgtDecisionModel` and implement the `decide` method.\n",
    "\n",
    "## The simulation model\n",
    "\n",
    "In the simulation model defined in our example, boids are initially located randomly in the environment with a random orientation and speed.\n",
    "\n",
    "## Launch the HTML runner\n",
    "\n",
    "Finally, we launch and configure the HTML runner "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {},
   "outputs": [
    {
     "name": "stderr",
     "output_type": "stream",
     "text": [
      "Resolving dependency: fr.univ-artois.lgi2a#similar2logo-lib;1.0-SNAPSHOT {default=[default]}\n",
      "Preparing to download artifact fr.univ-artois.lgi2a#similar2logo-lib;1.0-SNAPSHOT!similar2logo-lib.jar\n",
      "Preparing to download artifact fr.univ-artois.lgi2a#similar2logo-kernel;1.0-SNAPSHOT!similar2logo-kernel.jar\n",
      "Preparing to download artifact com.sparkjava#spark-core;2.8.0!spark-core.jar(bundle)\n",
      "Preparing to download artifact org.slf4j#slf4j-api;1.7.25!slf4j-api.jar\n",
      "Preparing to download artifact org.slf4j#jul-to-slf4j;1.7.25!jul-to-slf4j.jar\n",
      "Preparing to download artifact org.slf4j#jcl-over-slf4j;1.7.25!jcl-over-slf4j.jar\n",
      "Preparing to download artifact org.slf4j#log4j-over-slf4j;1.7.25!log4j-over-slf4j.jar\n",
      "Preparing to download artifact org.slf4j#slf4j-jdk14;1.7.25!slf4j-jdk14.jar\n",
      "Preparing to download artifact org.apache.commons#commons-math3;3.6.1!commons-math3.jar\n",
      "Preparing to download artifact fr.univ-artois.lgi2a#similar-microKernel;1.0-SNAPSHOT!similar-microKernel.jar\n",
      "Preparing to download artifact fr.univ-artois.lgi2a#similar-microKernel-commonLibs;1.0-SNAPSHOT!similar-microKernel-commonLibs.jar\n",
      "Preparing to download artifact fr.univ-artois.lgi2a#similar-extendedKernel;1.0-SNAPSHOT!similar-extendedKernel.jar\n",
      "Preparing to download artifact fr.univ-artois.lgi2a#similar-extendedKernel-extendedLibs;1.0-SNAPSHOT!similar-extendedKernel-extendedLibs.jar\n",
      "Preparing to download artifact org.json#json;20170516!json.jar(bundle)\n",
      "Preparing to download artifact net.jafama#jafama;2.3.1!jafama.jar\n",
      "Preparing to download artifact org.eclipse.jetty#jetty-server;9.4.12.v20180830!jetty-server.jar\n",
      "Preparing to download artifact org.eclipse.jetty#jetty-webapp;9.4.12.v20180830!jetty-webapp.jar\n",
      "Preparing to download artifact org.eclipse.jetty.websocket#websocket-server;9.4.12.v20180830!websocket-server.jar\n",
      "Preparing to download artifact org.eclipse.jetty.websocket#websocket-servlet;9.4.12.v20180830!websocket-servlet.jar\n",
      "Preparing to download artifact javax.servlet#javax.servlet-api;3.1.0!javax.servlet-api.jar\n",
      "Preparing to download artifact org.eclipse.jetty#jetty-http;9.4.12.v20180830!jetty-http.jar\n",
      "Preparing to download artifact org.eclipse.jetty#jetty-io;9.4.12.v20180830!jetty-io.jar\n",
      "Preparing to download artifact org.eclipse.jetty#jetty-util;9.4.12.v20180830!jetty-util.jar\n",
      "Preparing to download artifact org.eclipse.jetty#jetty-xml;9.4.12.v20180830!jetty-xml.jar\n",
      "Preparing to download artifact org.eclipse.jetty#jetty-servlet;9.4.12.v20180830!jetty-servlet.jar\n",
      "Preparing to download artifact org.eclipse.jetty#jetty-security;9.4.12.v20180830!jetty-security.jar\n",
      "Preparing to download artifact org.eclipse.jetty.websocket#websocket-common;9.4.12.v20180830!websocket-common.jar\n",
      "Preparing to download artifact org.eclipse.jetty.websocket#websocket-client;9.4.12.v20180830!websocket-client.jar\n",
      "Preparing to download artifact org.eclipse.jetty.websocket#websocket-api;9.4.12.v20180830!websocket-api.jar\n",
      "Preparing to download artifact org.eclipse.jetty#jetty-client;9.4.12.v20180830!jetty-client.jar\n"
     ]
    },
    {
     "data": {
      "text/plain": [
       "No Outputs"
      ]
     },
     "execution_count": 1,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "#@repository(\"~/.m2/repository/\")\n",
    "#@dependency(group=\"fr.univ-artois.lgi2a\", module=\"similar2logo-lib\", version=\"1.0-SNAPSHOT\")\n",
    "\n",
    "\n",
    "import static java.lang.Math.*\n",
    "import fr.univ_artois.lgi2a.similar.extendedkernel.libs.random.PRNG\n",
    "import fr.univ_artois.lgi2a.similar.extendedkernel.libs.abstractimpl.AbstractAgtDecisionModel\n",
    "import fr.univ_artois.lgi2a.similar.extendedkernel.simulationmodel.ISimulationParameters\n",
    "import fr.univ_artois.lgi2a.similar.microkernel.AgentCategory\n",
    "import fr.univ_artois.lgi2a.similar.microkernel.LevelIdentifier\n",
    "import fr.univ_artois.lgi2a.similar.microkernel.SimulationTimeStamp\n",
    "import fr.univ_artois.lgi2a.similar.microkernel.ISimulationModel.AgentInitializationData\n",
    "import fr.univ_artois.lgi2a.similar.microkernel.agents.IGlobalState\n",
    "import fr.univ_artois.lgi2a.similar.microkernel.agents.ILocalStateOfAgent\n",
    "import fr.univ_artois.lgi2a.similar.microkernel.agents.IPerceivedData\n",
    "import fr.univ_artois.lgi2a.similar.microkernel.influences.InfluencesMap\n",
    "import fr.univ_artois.lgi2a.similar.microkernel.levels.ILevel\n",
    "import fr.univ_artois.lgi2a.similar2logo.kernel.initializations.AbstractLogoSimulationModel\n",
    "import fr.univ_artois.lgi2a.similar2logo.kernel.model.LogoSimulationParameters\n",
    "import fr.univ_artois.lgi2a.similar2logo.kernel.model.Parameter\n",
    "import fr.univ_artois.lgi2a.similar2logo.kernel.model.agents.turtle.TurtleAgentCategory\n",
    "import fr.univ_artois.lgi2a.similar2logo.kernel.model.agents.turtle.TurtleFactory\n",
    "import fr.univ_artois.lgi2a.similar2logo.kernel.model.influences.ChangeDirection\n",
    "import fr.univ_artois.lgi2a.similar2logo.kernel.model.influences.ChangeSpeed\n",
    "import fr.univ_artois.lgi2a.similar2logo.kernel.model.levels.LogoSimulationLevelList\n",
    "import fr.univ_artois.lgi2a.similar2logo.lib.model.ConeBasedPerceptionModel\n",
    "import fr.univ_artois.lgi2a.similar2logo.lib.tools.html.Similar2LogoHtmlRunner\n",
    "import fr.univ_artois.lgi2a.similar2logo.lib.tools.math.MeanAngle\n",
    "import fr.univ_artois.lgi2a.similar2logo.kernel.tools.MathUtil\n",
    "\n",
    "//Define the parameters of the simulation\n",
    "def parameters = new LogoSimulationParameters() {\n",
    "    \n",
    "    @Parameter(name = \"repulsion distance\", description = \"the repulsion distance\")\n",
    "    public double repulsionDistance = 1\n",
    "\n",
    "    @Parameter(name = \"orientation distance\", description = \"the orientation distance\")\n",
    "    public double orientationDistance = 2\n",
    "    \n",
    "    @Parameter(name = \"attraction distance\", description = \"the attraction distance\")\n",
    "    public double attractionDistance = 4\n",
    "    \n",
    "    @Parameter(name = \"repulsion weight\", description = \"the repulsion weight\")\n",
    "    public double repulsionWeight = 10\n",
    "\n",
    "    @Parameter(name = \"orientation weight\", description = \"the orientation weight\")\n",
    "    public double orientationWeight = 20\n",
    "    \n",
    "    @Parameter(name = \"attraction weight\", description = \"the attraction weight\")\n",
    "    public double attractionWeight = 0.1\n",
    "\n",
    "    @Parameter(name = \"maximal initial speed\", description = \"the maximal initial speed\")\n",
    "    public double maxInitialSpeed = 2\n",
    "\n",
    "    @Parameter(name = \"minimal initial speed\", description = \"the minimal initial speed\")\n",
    "    public double minInitialSpeed = 1\n",
    "\n",
    "    @Parameter(name = \"perception angle\", description = \"the perception angle in rad\")\n",
    "    public double perceptionAngle = PI\n",
    "\n",
    "    @Parameter(name = \"number of agents\", description = \"the number of boids in the simulation\")\n",
    "    public int nbOfAgents = 2000\n",
    "\n",
    "    @Parameter(name = \"max angular speed\", description = \"the maximal angular speed in rad/step\")\n",
    "    public double maxAngle = PI/4\n",
    "}\n",
    "\n",
    "//Define the decision model of a boid\n",
    "def decisionModel = new AbstractAgtDecisionModel(LogoSimulationLevelList.LOGO) {\n",
    "    void decide(\n",
    "        SimulationTimeStamp s, //the current simulation step\n",
    "        SimulationTimeStamp ns, //the next simulation step\n",
    "        IGlobalState gs, //the global state of the agent\n",
    "        ILocalStateOfAgent pls, //the public local state of the boid\n",
    "        ILocalStateOfAgent prls, //the private local state of the boid\n",
    "        IPerceivedData pd, //the data perceived by the boid\n",
    "        InfluencesMap i //the influences produced by the boid\n",
    "    ) {\n",
    "        if(!pd.turtles.empty) {\n",
    "            def sc = 0, //the speed command\n",
    "                meanAngle = new MeanAngle(),//the orientation command\n",
    "                n = 0 //the number of boids in the orientation area\n",
    "            //computes the commands according to the area in which the perceived boid is located \n",
    "            pd.turtles.each{ boid ->\n",
    "                switch(boid.distanceTo) {\n",
    "                    //the repulsion area\n",
    "                    case {it <= parameters.repulsionDistance}:\n",
    "                        meanAngle.add(pls.direction - boid.directionTo, parameters.repulsionWeight)\n",
    "                        break\n",
    "                    //the orientation area\n",
    "                    case {it > parameters.repulsionDistance && it <= parameters.orientationDistance}:\n",
    "                        meanAngle.add(boid.content.direction - pls.direction,parameters.orientationWeight)\n",
    "                        sc+=boid.content.speed - pls.speed\n",
    "                        n++\n",
    "                        break\n",
    "                    //the attraction area\n",
    "                    case {it > parameters.orientationDistance && it <= parameters.attractionDistance}:\n",
    "                        meanAngle.add(boid.directionTo- pls.direction, parameters.attractionWeight)\n",
    "                        break\n",
    "                }\n",
    "            }\n",
    "            //the orientation command\n",
    "            def oc = meanAngle.value()\n",
    "            if (!MathUtil.areEqual(oc, 0)) {\n",
    "                //ceil the orientation command\n",
    "                if(abs(oc) > parameters.maxAngle) oc = signum(oc)*parameters.maxAngle\n",
    "                //emit a change direction influence\n",
    "                i.add new ChangeDirection(s, ns, oc, pls)\n",
    "            }\n",
    "            //emit a change speed influence\n",
    "            if (n > 0) i.add new ChangeSpeed(s, ns, sc/n, pls)\n",
    "        }\n",
    "    }\n",
    "}\n",
    "\n",
    "//Define the initial state of the simulation\n",
    "def simulationModel = new AbstractLogoSimulationModel(parameters) {\n",
    "    \n",
    "    //Generate the agents\n",
    "    protected AgentInitializationData generateAgents(\n",
    "        ISimulationParameters p,\n",
    "        Map<LevelIdentifier, ILevel> l\n",
    "    ) {\n",
    "        def result = new AgentInitializationData()\n",
    "        //For each boid to be generated\n",
    "        p.nbOfAgents.times {\n",
    "            //generate the boid\n",
    "            result.agents.add TurtleFactory.generate(\n",
    "                new ConeBasedPerceptionModel(p.attractionDistance,p.perceptionAngle,true,false,false), //the perception model\n",
    "                decisionModel, //the decision model\n",
    "                new AgentCategory(\"b\", TurtleAgentCategory.CATEGORY), //the category\n",
    "                PRNG.get().randomAngle(), //the initial orientation\n",
    "                p.minInitialSpeed + PRNG.get().randomDouble()*(p.maxInitialSpeed-p.minInitialSpeed),//the initial speed\n",
    "                0,//the initial acceleration\n",
    "                PRNG.get().randomDouble()*p.gridWidth,//the initial x position\n",
    "                PRNG.get().randomDouble()*p.gridHeight //the initial y position\n",
    "            )\n",
    "        }\n",
    "        return result\n",
    "    }\n",
    "}\n",
    "\n",
    "// Creation of the runner\n",
    "def runner = new Similar2LogoHtmlRunner( )\n",
    "\n",
    "// Configuration of the runner\n",
    "runner.config.exportAgents = true\n",
    "\n",
    "// Initialize the runner\n",
    "runner.initializeRunner simulationModel\n",
    "\n",
    "// Open the GUI\n",
    "runner.showView( )"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "SciJava",
   "language": "groovy",
   "name": "scijava"
  },
  "language_info": {
   "codemirror_mode": "groovy",
   "file_extension": "",
   "mimetype": "",
   "name": "scijava",
   "nbconverter_exporter": "",
   "pygments_lexer": "groovy",
   "version": "1.0"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
