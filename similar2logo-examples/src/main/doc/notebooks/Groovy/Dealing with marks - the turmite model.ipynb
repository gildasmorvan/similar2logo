{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Dealing with marks: the turmite model\n",
    "\n",
    "The [turmite model](https://en.wikipedia.org/wiki/Langton's_ant), developed by [Christopher Langton](https://en.wikipedia.org/wiki/Christopher_Langton) in 1986, is a very simple mono-agent model that exhibits an emergent behavior. It is based on 2 rules:\n",
    "\n",
    "* If the turmite is on a patch that does not contain a mark, it turns right, drops a mark, and moves forward,\n",
    "\n",
    "* If the turmite is on a patch that contains a mark, it turns left, removes the mark, and moves forward.\n",
    "\n",
    "## Model parameters\n",
    "\n",
    "First we define the simulation parameters. Here we only need to specify the final step of the simulation.\n",
    "\n",
    "## Decision model\n",
    "\n",
    "The decision model implements the above described rules.\n",
    "\n",
    "## Simulation model\n",
    "\n",
    "The simulation model generates a turmite heading north at the location 10.5,10.5 with a speed of 1 and an acceleration of 0.\n",
    "\n",
    "## Launch the HTML runner\n",
    "\n",
    "In this case we want to observe turtles and marks."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "metadata": {},
   "outputs": [
    {
     "name": "stderr",
     "output_type": "stream",
     "text": [
      "Resolving dependency: fr.univ_artois.lgi2a#similar2logo-lib;1.0-SNAPSHOT {default=[default]}\n",
      "Preparing to download artifact fr.univ_artois.lgi2a#similar2logo-lib;1.0-SNAPSHOT!similar2logo-lib.jar\n",
      "Preparing to download artifact fr.univ_artois.lgi2a#similar2logo-kernel;1.0-SNAPSHOT!similar2logo-kernel.jar\n",
      "Preparing to download artifact com.sparkjava#spark-core;2.7.2!spark-core.jar(bundle)\n",
      "Preparing to download artifact org.slf4j#slf4j-simple;1.7.13!slf4j-simple.jar\n",
      "Preparing to download artifact org.apache.commons#commons-math3;3.6.1!commons-math3.jar\n",
      "Preparing to download artifact fr.univ_artois.lgi2a#similar-microKernel;1.0-SNAPSHOT!similar-microKernel.jar\n",
      "Preparing to download artifact fr.univ_artois.lgi2a#similar-microKernel-commonLibs;1.0-SNAPSHOT!similar-microKernel-commonLibs.jar\n",
      "Preparing to download artifact fr.univ_artois.lgi2a#similar-extendedKernel;1.0-SNAPSHOT!similar-extendedKernel.jar\n",
      "Preparing to download artifact fr.univ_artois.lgi2a#similar-extendedKernel-extendedLibs;1.0-SNAPSHOT!similar-extendedKernel-extendedLibs.jar\n",
      "Preparing to download artifact org.json#json;20170516!json.jar(bundle)\n",
      "Preparing to download artifact net.jafama#jafama;2.3.1!jafama.jar\n",
      "Preparing to download artifact org.slf4j#slf4j-api;1.7.13!slf4j-api.jar\n",
      "Preparing to download artifact org.eclipse.jetty#jetty-server;9.4.8.v20171121!jetty-server.jar\n",
      "Preparing to download artifact org.eclipse.jetty#jetty-webapp;9.4.8.v20171121!jetty-webapp.jar\n",
      "Preparing to download artifact org.eclipse.jetty.websocket#websocket-server;9.4.8.v20171121!websocket-server.jar\n",
      "Preparing to download artifact org.eclipse.jetty.websocket#websocket-servlet;9.4.8.v20171121!websocket-servlet.jar\n",
      "Preparing to download artifact javax.servlet#javax.servlet-api;3.1.0!javax.servlet-api.jar\n",
      "Preparing to download artifact org.eclipse.jetty#jetty-http;9.4.8.v20171121!jetty-http.jar\n",
      "Preparing to download artifact org.eclipse.jetty#jetty-io;9.4.8.v20171121!jetty-io.jar\n",
      "Preparing to download artifact org.eclipse.jetty#jetty-util;9.4.8.v20171121!jetty-util.jar\n",
      "Preparing to download artifact org.eclipse.jetty#jetty-xml;9.4.8.v20171121!jetty-xml.jar\n",
      "Preparing to download artifact org.eclipse.jetty#jetty-servlet;9.4.8.v20171121!jetty-servlet.jar\n",
      "Preparing to download artifact org.eclipse.jetty#jetty-security;9.4.8.v20171121!jetty-security.jar\n",
      "Preparing to download artifact org.eclipse.jetty.websocket#websocket-common;9.4.8.v20171121!websocket-common.jar\n",
      "Preparing to download artifact org.eclipse.jetty.websocket#websocket-client;9.4.8.v20171121!websocket-client.jar\n",
      "Preparing to download artifact org.eclipse.jetty.websocket#websocket-api;9.4.8.v20171121!websocket-api.jar\n",
      "Preparing to download artifact org.eclipse.jetty#jetty-client;9.4.8.v20171121!jetty-client.jar\n"
     ]
    },
    {
     "data": {
      "text/plain": [
       "No Outputs"
      ]
     },
     "execution_count": 2,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "#@repository(\"~/.m2/repository/\")\n",
    "#@dependency(group=\"fr.univ_artois.lgi2a\", module=\"similar2logo-lib\", version=\"1.0-SNAPSHOT\")\n",
    "\n",
    "\n",
    "import static java.lang.Math.*\n",
    "import java.awt.geom.Point2D\n",
    "\n",
    "import fr.univ_artois.lgi2a.similar.extendedkernel.libs.abstractimpl.AbstractAgtDecisionModel\n",
    "import fr.univ_artois.lgi2a.similar.extendedkernel.simulationmodel.ISimulationParameters\n",
    "import fr.univ_artois.lgi2a.similar.microkernel.AgentCategory\n",
    "import fr.univ_artois.lgi2a.similar.microkernel.LevelIdentifier\n",
    "import fr.univ_artois.lgi2a.similar.microkernel.SimulationTimeStamp\n",
    "import fr.univ_artois.lgi2a.similar.microkernel.ISimulationModel.AgentInitializationData\n",
    "import fr.univ_artois.lgi2a.similar.microkernel.agents.IAgent4Engine\n",
    "import fr.univ_artois.lgi2a.similar.microkernel.agents.IGlobalState\n",
    "import fr.univ_artois.lgi2a.similar.microkernel.agents.ILocalStateOfAgent\n",
    "import fr.univ_artois.lgi2a.similar.microkernel.agents.IPerceivedData\n",
    "import fr.univ_artois.lgi2a.similar.microkernel.influences.InfluencesMap\n",
    "import fr.univ_artois.lgi2a.similar.microkernel.levels.ILevel\n",
    "import fr.univ_artois.lgi2a.similar2logo.kernel.initializations.AbstractLogoSimulationModel\n",
    "import fr.univ_artois.lgi2a.similar2logo.kernel.model.LogoSimulationParameters\n",
    "import fr.univ_artois.lgi2a.similar2logo.kernel.model.agents.turtle.TurtleAgentCategory\n",
    "import fr.univ_artois.lgi2a.similar2logo.kernel.model.agents.turtle.TurtleFactory\n",
    "import fr.univ_artois.lgi2a.similar2logo.kernel.model.environment.LogoEnvPLS\n",
    "import fr.univ_artois.lgi2a.similar2logo.kernel.model.environment.Mark\n",
    "import fr.univ_artois.lgi2a.similar2logo.kernel.model.influences.ChangeDirection\n",
    "import fr.univ_artois.lgi2a.similar2logo.kernel.model.influences.DropMark\n",
    "import fr.univ_artois.lgi2a.similar2logo.kernel.model.influences.RemoveMark\n",
    "import fr.univ_artois.lgi2a.similar2logo.kernel.model.levels.LogoSimulationLevelList\n",
    "import fr.univ_artois.lgi2a.similar2logo.lib.model.ConeBasedPerceptionModel\n",
    "import fr.univ_artois.lgi2a.similar2logo.lib.probes.LogoRealTimeMatcher\n",
    "import fr.univ_artois.lgi2a.similar2logo.lib.tools.html.Similar2LogoHtmlRunner\n",
    "\n",
    "//Define the parameters of the simulation\n",
    "def parameters = new LogoSimulationParameters(\n",
    "    finalTime: new SimulationTimeStamp(100000)\n",
    ")\n",
    "\n",
    "//Define the decision model of the agents\n",
    "def decisionModel = new AbstractAgtDecisionModel(LogoSimulationLevelList.LOGO) {\n",
    "    void decide(\n",
    "        SimulationTimeStamp s, //the current simulation step\n",
    "        SimulationTimeStamp ns, //the next simulation step\n",
    "        IGlobalState gs, //the global state of the agent\n",
    "        ILocalStateOfAgent pls, //the public local state of the agent\n",
    "        ILocalStateOfAgent prls, //the private local state of the agent\n",
    "        IPerceivedData pd, //the data perceived by the agent\n",
    "        InfluencesMap i //the influences produced by the agent\n",
    "    ) {\n",
    "        if(pd.marks.empty) i.with { //if the agent perceives no mark\n",
    "            add new ChangeDirection(s, ns, PI/2, pls) //it turns pi/2 rad\n",
    "            add new DropMark(s, ns, new Mark((Point2D) pls.location.clone(), null)) //and drops a mark\n",
    "        } else i.with { //if the agent perceives a mark\n",
    "            add new ChangeDirection(s, ns, -PI/2, pls) //it turns -pi/2 rad\n",
    "            add new RemoveMark(s, ns,pd.marks.iterator().next().content) //and removes the mark\n",
    "        }\n",
    "    }\n",
    "}\n",
    "\n",
    "//Define the initial state of the simulation\n",
    "def simulationModel = new AbstractLogoSimulationModel(parameters) {\n",
    "    \n",
    "    //Generate the agents\n",
    "    protected AgentInitializationData generateAgents(\n",
    "        ISimulationParameters simulationParameters, //the parameters of the simulation\n",
    "        Map<LevelIdentifier, ILevel> levels //the levels of the simulation\n",
    "    ) {\n",
    "        //Create a new turmite agent\n",
    "        def turmite = TurtleFactory.generate(\n",
    "            new ConeBasedPerceptionModel(0, 2*Math.PI, false, true, false), //a perception model that allows to perceive marks\n",
    "            decisionModel, //the turmite decision model\n",
    "            new AgentCategory(\"turmite\", TurtleAgentCategory.CATEGORY), //the turmite category\n",
    "            LogoEnvPLS.NORTH, //heading north\n",
    "            1, //a speed of 1\n",
    "            0, //an acceleration of 0\n",
    "            10.5, 10.5 //located at 10.5, 10.5\n",
    "        ),\n",
    "        result = new AgentInitializationData() //create the agent initialization data\n",
    "        result.agents.add turmite //add the turmite agent\n",
    "        return result\n",
    "    }\n",
    "}\n",
    "\n",
    "// Creation of the runner\n",
    "def runner = new Similar2LogoHtmlRunner( )\n",
    "\n",
    "// Configuration of the runner\n",
    "runner.config.exportAgents = true\n",
    "runner.config.exportMarks = true\n",
    "\n",
    "// Initialize the runner\n",
    "runner.initializeRunner simulationModel\n",
    "\n",
    "//Add a real time matcher probe\n",
    "runner.addProbe \"Real time matcher\", new LogoRealTimeMatcher(20)\n",
    "\n",
    "// Open the GUI\n",
    "runner.showView( )"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "SciJava",
   "language": "groovy",
   "name": "scijava"
  },
  "language_info": {
   "codemirror_mode": "groovy",
   "file_extension": "",
   "mimetype": "",
   "name": "scijava",
   "nbconverter_exporter": "",
   "pygments_lexer": "groovy",
   "version": "1.0"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
